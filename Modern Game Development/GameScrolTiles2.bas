10 COLOR 2, 1, 1
20 SCREEN 4,2,0
40 DEFINT A-Z

90 REM load patterns
100 FOR J=0 TO 63
110   READ R
120   VPOKE J, R
121   VPOKE &H800+J, R
122   VPOKE &H1000+J, R
140 NEXT J

150 REM load colors
160 FOR I=0 TO 7
165   READ R
170   FOR J=0 TO 7
180     VPOKE &H2000+I*8+J, R
181     VPOKE &H2800+I*8+J, R
182     VPOKE &H3000+I*8+J, R
190   NEXT J
195 NEXT I

500 REM Load name table
510 FOR Y=0 TO 23
520   FOR X=0 TO 31
530     VPOKE &H1800+X+Y*&H20, RND(1)*8
540   NEXT X
550 NEXT Y

600 REM scroll right
605 A$=INPUT$(1)
610 IF A$="1" THEN GOSUB 750: REM Scroll using name table move
620 IF A$="2" THEN GOSUB 700: REM Scroll using MSXBAS2ROM specific funtions
630 GOTO 600

700 ' MOVING RIGHT USING MSXBAS2ROM FUNCTIONS
701 DIM SC%(15, 23) : ' 32 cols (0-15: 2 cols for each integer), 24 rows (0-23)
702 SCREEN COPY TO SC% SCROLL 7 : ' copy to array and do a scroll on the data copied
710 FOR Y=0 TO 23
711 TE% = SC%(15,Y)
712 SC%(15,Y) = ((TE% \ 256) * 256) + RND(1)*8
714 NEXT Y
720 SCREEN PASTE FROM SC% : ' copy from array to screen
740 RETURN

750 ' MOVING RIGHT
760 FOR Y=0 TO 23
770   FOR X=31 TO 1 STEP -1
780     T=VPEEK (&H17FF+X+Y*&H20)
790     VPOKE &H1800+X+Y*&H20, T
800   NEXT X
810 NEXT Y
820 FOR Y=0 TO 23
830   VPOKE &H1800+Y*&H20, RND(1)*8
840 NEXT Y
850 RETURN

1000 DATA &HFF, &HFF, &HBF, &H9F, &H8F, &H87, &H83, &H81
1010 DATA &HFF, &HFF, &HFD, &HF9, &HF1, &HE1, &HC1, &H81
1020 DATA &H81, &H83, &H87, &H8F, &H9F, &HBF, &HFF, &HFF
1030 DATA &H81, &HC1, &HE1, &HF1, &HF9, &HFD, &HFF, &HFF
1040 DATA &H90, &H40, &H00, &H40, &H00, &H10, &H00, &H82
1050 DATA &H90, &H40, &H00, &H40, &H00, &H10, &H00, &H82
1060 DATA &H50, &H20, &H00, &H48, &H20, &H90, &H40, &H40
1070 DATA &H80, &H20, &H80, &H00, &H41, &H00, &H84, &H10

2040 DATA &H45, &H45, &H45, &H45, &H23, &H23, &HAB, &HAB
